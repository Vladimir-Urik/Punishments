plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'lol.gggedr.punishments'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'

    // Reflection
    implementation 'org.reflections:reflections:0.10.2'

    // MongoDB
    implementation 'org.mongodb:mongo-java-driver:2.12.3'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

var buildNumber = System.getenv('BUILD_NUMBER') ?: 'DEV'

tasks.jar.configure {
    manifest {
        attributes 'Author': 'GGGEDR <Vladimir-Urik>'
        attributes 'Plugin-Version': version
        attributes 'Build-Number': buildNumber
        attributes 'Build-Date': new Date().format('dd.MM.yyyy HH:mm:ss')
    }

    dependsOn(tasks.shadowJar)
    enabled = false
}

shadowJar {
    archiveBaseName.set('Punishments')
    archiveClassifier.set('')
    archiveVersion.set(version +'-'+ buildNumber)
}

processResources {
    def props = [
            version: version,
            buildNumber: buildNumber,
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
